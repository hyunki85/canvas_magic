apply plugin: 'com.android.application'
apply from: '../config/quality/quality.gradle'
apply plugin: 'jacoco'
apply plugin: 'com.github.ben-manes.versions'
//apply plugin: 'io.fabric'

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.3'

    dexOptions {
        maxProcessCount 4
        preDexLibraries false
        javaMaxHeapSize "8g"
    }

    defaultConfig {
        defaultPublishConfig 'debug'

        applicationId "com.h2play.canvas_magic"
        minSdkVersion 21
        targetSdkVersion 30
        testInstrumentationRunner "${applicationId}.runner.RxAndroidJUnitRunner"
        versionCode 1000000
        // Major -> Millions, Minor -> Thousands, Bugfix -> Hundreds. E.g 1.3.72 == 1,003,072
        versionName '1.0.0'

        javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true;

        buildConfigField("String", "POKEAPI_API_URL",
                "\"${PokeapiApiUrl}\"")
    }
    buildTypes {
        debug {
            minifyEnabled false
            multiDexEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    lintOptions {
        textOutput "stdout"
        textReport true
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        abortOnError false
        lintConfig file("$projectDir/lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
            all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.squareup.okio:okio:1.11.0'
        force "com.squareup.okhttp3:okhttp:3.5.0"
    }
}


apply from: 'dependencies.gradle'
apply plugin: 'com.google.gms.google-services'

dependencies {

    implementation networkLibs
    implementation rxJavaLibs

    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
        exclude group: 'glide-parent'
    }

    implementation 'com.andrognito.pinlockview:pinlockview:2.1.0'
    implementation 'com.github.madrapps:pikolo:1.1.6'

    implementation 'androidx.appcompat:appcompat:1.0.0-alpha1'
    implementation 'com.google.android.material:material:1.1.0'

    implementation 'com.f2prateek.dart:dart:2.0.2'
    implementation 'com.google.firebase:firebase-core:16.0.4'
    implementation 'com.google.firebase:firebase-ads:17.0.0'
    implementation 'com.google.firebase:firebase-database:16.0.4'
    annotationProcessor 'com.f2prateek.dart:dart-processor:2.0.2'

    implementation 'com.github.QuadFlask:colorpicker:0.0.13'

    implementation 'jp.wasabeef:glide-transformations:3.0.1'


    implementation 'com.google.code.gson:gson:2.8.6'

    //RxPermission for easy runtime permission management
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.4@aar'

    implementation "com.jakewharton:butterknife:$versions.butterKnife"
    implementation "com.jakewharton.timber:timber:4.5.1"
    implementation "com.facebook.stetho:stetho-okhttp3:1.5.0"

    debugImplementation 'com.readystatesoftware.chuck:library:1.0.4'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.0.4'

    debugImplementation('com.github.ajitsing:sherlock:1.0.2@aar') {
        transitive = true
    }
    releaseImplementation('com.github.ajitsing:sherlock-no-op:1.0.2@aar')

    implementation 'com.tspoon.traceur:traceur:1.0.1'

    implementation "com.google.dagger:dagger:$versions.dagger"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2

    debugImplementation leakCannary
    releaseImplementation leakCannary
    testImplementation leakCannary

    // APT dependencies
    annotationProcessor "com.jakewharton:butterknife-compiler:$versions.butterKnife"
    annotationProcessor daggerCompiler
    testAnnotationProcessor daggerCompiler
    androidTestAnnotationProcessor daggerCompiler

    testImplementation unitTestLibs
    androidTestImplementation androidTestsLibs
    implementation "androidx.constraintlayout:constraintlayout:2.0.0"
    implementation 'com.google.firebase:firebase-firestore:21.4.0'
    implementation 'com.google.firebase:firebase-auth:19.2.0'


}

// Log out test results to console
tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}
